
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВидСравнения = ">=";
	ТипДлительности = "Миллисекунды";
	Длительность = 1000;
	
	УстановитьПутьКЖурналу();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = "Журнал" И ИмяСобытия = "УдалениеЗаписи" Тогда
		ОбновитьНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКЖурналуНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	 СтандартнаяОбработка = ЛОЖЬ;
    
    Режим = РежимДиалогаВыбораФайла.ВыборКаталога; 
    ДиалогОткрытия = Новый ДиалогВыбораФайла(Режим); 
    ДиалогОткрытия.Каталог = "";  
    ДиалогОткрытия.Заголовок = "Выберите каталог записи журнала"; 
        
    Параметр = "";
    Оповещение = Новый ОписаниеОповещения("ВыборКаталогаЗавершение", ЭтотОбъект, Параметр );
    ДиалогОткрытия.Показать(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаЗавершение(Результат, Параметр) Экспорт
    
    Если Результат = Неопределено Тогда
         Сообщить("Каталог не выбран");
         Возврат;
    КонецЕсли;
    
    ПутьКЖурналу = Результат[0];
    
КонецПроцедуры

&НаКлиенте
Процедура ЗамерПриИзменении(Элемент)
	УстановитьПутьКЖурналу();
	
	ОбновитьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	ОбновитьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	ОбновитьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура СТекстомЗапросаПриИзменении(Элемент)
	ОбновитьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоДлительностиПриИзменении(Элемент)
	ОбновитьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ТипДлительностиПриИзменении(Элемент)
	ОбновитьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ВидСравненияПриИзменении(Элемент)
	ОбновитьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура Длительность1ПриИзменении(Элемент)
	ОбновитьНаКлиенте();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НовыйЗамер(Команда)
	Оповещение = Новый ОписаниеОповещения("ВводСтрокиЗавершение", ЭтотОбъект);
	ПоказатьВводСтроки(Оповещение, "", "Имя замера", 20, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ВводСтрокиЗавершение(Результат, ДопПараметры) Экспорт
	Если Не ПустаяСтрока(Результат) Тогда
		Замер = СоздатьЗамер(Результат);
		
		ОбновитьНаКлиенте();
	КонецЕсли; 
КонецПроцедуры
	
&НаКлиенте
Процедура Очистить(Команда)
	ОчиститьЖурнал(Замер);
	
	ОбновитьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьТЖ(Команда)
	ПрочитатьТЖНаСервере();
	
	ОбновитьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЗапросы1С(Команда)
	ПолучитьЗапросы1СНаСервере();
	
	ОбновитьНаКлиенте();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьНаКлиенте()
	УстановитьОтбор();
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтбор()
	
	Список.Отбор.Элементы.Очистить();
	
	Если ЗначениеЗаполнено(Замер) Тогда
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Замер");
		ЭлементОтбора.ПравоеЗначение = Замер;
		ЭлементОтбора.Использование = Истина;		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаНачала) Тогда
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаЗаписи");
		ЭлементОтбора.ПравоеЗначение = ДатаНачала;
		ЭлементОтбора.Использование = Истина;		
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ДатаОкончания) Тогда
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаЗаписи");
		ЭлементОтбора.ПравоеЗначение = ДатаОкончания;
		ЭлементОтбора.Использование = Истина;		
	КонецЕсли;
			
	Если СТекстомЗапроса Тогда
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Sql");
		ЭлементОтбора.Использование = Истина;		
	КонецЕсли;	
	
	//Длительность
	Если ОтборПоДлительности Тогда
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Если ВидСравнения = ">=" Тогда
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
		ИначеЕсли ВидСравнения = "<=" Тогда
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
		КонецЕсли;
		Если ТипДлительности = "Микросекунды" Тогда
			ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Длительность");
		ИначеЕсли ТипДлительности = "Миллисекунды" Тогда
			ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДлительностьМиллиСек");
		ИначеЕсли ТипДлительности = "Секунды" Тогда
			ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДлительностьСек");
		КонецЕсли;
		ЭлементОтбора.ПравоеЗначение = Длительность;
		ЭлементОтбора.Использование = Истина;				
	КонецЕсли;
			
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьЗамер(Наименование)
		НовыйЗамер = Справочники.Замеры.СоздатьЭлемент();
		НовыйЗамер.Наименование = Наименование;
		НовыйЗамер.Записать();
		
		Возврат НовыйЗамер.Ссылка;
КонецФункции

&НаСервереБезКонтекста
Процедура ОчиститьЖурнал(Замер)
	НаборЗаписей = РегистрыСведений.Журнал.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Замер.Установить(Замер);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать();	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьТЖНаСервере()
	Если Очищать Тогда
		ОчиститьЖурнал(Замер);
	КонецЕсли;
	
	Если Не ПустаяСтрока(usr) Или Не ПустаяСтрока(DataBase) Или ТолькоСSQL Тогда
		Фильтры = Новый Структура;
		Фильтры.Вставить("usr", usr);
		Фильтры.Вставить("DataBase", DataBase);
		Фильтры.Вставить("ТолькоСSQL", ТолькоСSQL);
	КонецЕсли;
	
	ТЖСервер.ПрочитатьЖурнал(ПутьКЖурналу, Замер, Фильтры);
КонецПроцедуры

&НаСервере
Процедура УстановитьПутьКЖурналу()
	Если ЗначениеЗаполнено(Замер) Тогда
		ПутьКЖурналу = Замер.ПутьКЖурналу;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗапросы1СНаСервере()
	РазборЗапросаСервер.ОбработатьЗаписи(Замер);
КонецПроцедуры
#КонецОбласти
