#Область ПрограммныйИнтерфейс

Процедура ОбработатьЗаписи(Замер) Экспорт
	НаборЗаписей = РегистрыСведений.Журнал.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Замер.Установить(Замер);
	НаборЗаписей.Прочитать();

	Для Каждого Запись Из НаборЗаписей Цикл
		Запись.Запрос1С =  РазобратьТекстЗапроса(Запись.Замер, Запись.Sql);
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
КонецПроцедуры	

Функция РазобратьТекстЗапроса(Замер, Знач ТекстЗапроса) Экспорт
	Если Не ЗначениеЗаполнено(Замер) Тогда
		Сообщить("Не задан Замер");
		
		Возврат "";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Замер.База) Тогда
		Сообщить("Не задана База");
		
		Возврат "";
	КонецЕсли;	
	
	ТаблицаСоответствия= Новый ТаблицаЗначений();
	ТаблицаСоответствия.Колонки.Добавить("ТаблицаSQL");
	ТаблицаСоответствия.Колонки.Добавить("Таблица1С");
	ТаблицаСоответствия.Колонки.Добавить("ПсевдонимSQL");	
	
	//Ищем таблицы в тексте
	ШаблоныТаблиц = ШаблоныПоискаТаблиц();
	Для Каждого Шаблон Из ШаблоныТаблиц Цикл

		РезультатПоиска  = СтрНайтиПоРегулярномуВыражению(ТекстЗапроса, Шаблон.Патерн);
		Если РезультатПоиска.НачальнаяПозиция <> 0 Тогда
			СтрокаТаблицыSQL = СокрЛП(Сред(РезультатПоиска.Значение, Шаблон.Длина, РезультатПоиска.Длина - 1));
			
			Имена = СтрРазделить(СтрокаТаблицыSQL, " ");
			
			Если Имена.Количество() = 2 Тогда
				ТаблицаSQL = Имена[0];
				ПсевдонимSQL = Имена[1];
			Иначе
				ТаблицаSQL = СтрокаТаблицыSQL;
				ПсевдонимSQL = "";
			КонецЕсли;
			
			Таблица1СИмя = "";
			Таблица1С= Справочники.Методанные1С.НайтиПоРеквизиту("ИмяТаблицыХранения", ТаблицаSQL, , Замер.База);
		
			НоваяСтрока = ТаблицаСоответствия.Добавить();
			НоваяСтрока.ТаблицаSQL = ТаблицаSQL;
			НоваяСтрока.Таблица1С = Таблица1С;
			НоваяСтрока.ПсевдонимSQL = ПсевдонимSQL;
		КонецЕсли;

	КонецЦикла;
	
	//Заменяем таблицы
	Для Каждого СоответствиеТаблиц Из ТаблицаСоответствия Цикл
		ТаблицаSQL = СоответствиеТаблиц.ТаблицаSQL;
		Таблица1С = СоответствиеТаблиц.Таблица1С;
		ПсевдонимSQL = СоответствиеТаблиц.ПсевдонимSQL;
		
		//Имена таблиц                                      
		ИмяТаблицы1С = Таблица1С.Метаданные+"."+Таблица1С.Назначение;
		ТекстЗапроса= СтрЗаменить(ТекстЗапроса, ТаблицаSQL, ИмяТаблицы1С);
		
		//Имена полей
		ЗаменитьПоляSQL(ТекстЗапроса, ТаблицаСоответствия);
		
		//Имена псевдонимов таблиц                                    
		ПсевдонимТаблицы1С = Таблица1С.Наименование+Таблица1С.Назначение;
		ТекстЗапроса= СтрЗаменить(ТекстЗапроса, ПсевдонимSQL, "КАК "+ ПсевдонимТаблицы1С);
	КонецЦикла;
	
	//Заменяем операторы
	Шаблоны = ШаблоныТерминов1С();
	Для Каждого Шаблон Из Шаблоны Цикл
		ИмяSQL = Шаблон.Ключ;
		Имя1С = Шаблон.Значение;
		
		ТекстЗапроса= СтрЗаменить(ТекстЗапроса, ИмяSQL, Имя1С);
	КонецЦикла;
	
	Возврат ТекстЗапроса;
КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ШаблоныПоискаТаблиц()
	Шаблоны = Новый Массив();
	Шаблоны.Добавить(Новый Структура("Патерн,Длина","FROM \w+ T\d", 5));
	Шаблоны.Добавить(Новый Структура("Патерн,Длина","JOIN \w+ T\d", 5));
	
	Возврат Шаблоны;
КонецФункции

Процедура ЗаменитьПоляSQL(Текст1С, ТаблицаСоответствия)
	Для Каждого стрСоответствия ИЗ ТаблицаСоответствия Цикл
		Таблица1С= стрСоответствия.Таблица1С;
		ПсевдонимSQL= стрСоответствия.ПсевдонимSQL;

		Выборка= Справочники.Поля.Выбрать(, Таблица1С);
		Пока Выборка.Следующий() Цикл     
		                     
			Если ПустаяСтрока(Выборка.Наименование) Тогда
				Продолжить;
			КонецЕсли;
			
			Текст1С= СтрЗаменить(Текст1С, ПсевдонимSQL+"."+Выборка.ИмяХранения, Таблица1С.Наименование+"."+Выборка.Наименование);
			
			//Заменим синонимы полей
			СинонимИмяХранения = Выборка.ИмяХранения;
			Если СтрНачинаетсяС(СинонимИмяХранения, "_") Тогда
				СинонимИмяХранения = Прав(Выборка.ИмяХранения, СтрДлина(Выборка.ИмяХранения)-1);
			КонецЕсли;
			Текст1С= СтрЗаменить(Текст1С, СинонимИмяХранения, Выборка.Наименование);
			
		КонецЦикла; 
	КонецЦикла;
КонецПроцедуры

Функция ШаблоныТерминов1С()
	Шаблоны = Новый Соответствие();
	Шаблоны.Вставить("SELECT", "ВЫБРАТЬ");
	Шаблоны.Вставить("FROM", "ИЗ");
	Шаблоны.Вставить("WHERE", "ГДЕ");
	Шаблоны.Вставить("ORDER BY", "СОРТИРОВАТЬ ПО");
	Шаблоны.Вставить("GROUP BY", "СГРУППИРОВАТЬ ПО");
	Шаблоны.Вставить("LIMIT ", "ЛИМИТ ");
	Шаблоны.Вставить("LEFT OUTER JOIN", "СОЕДИНЕНИЕ ЛЕВОЕ");
	Шаблоны.Вставить("INNER JOIN", "СОЕДИНЕНИЕ ВНУТРЕННЕЕ");
	Шаблоны.Вставить("AND ", "И ");
	Шаблоны.Вставить("OR ", "ИЛИ ");
	Шаблоны.Вставить("TRUE", "Истина");
	Шаблоны.Вставить("FALSE", "Ложь");
	Шаблоны.Вставить("INSERT", "ВСТАВИТЬ");
	Шаблоны.Вставить("UPDATE", "ОБНОВИТЬ");
	Шаблоны.Вставить("DELETE", "УДАЛИТЬ");	
	Шаблоны.Вставить("ON ", " ПО ");
	Шаблоны.Вставить("CASE WHEN", "ВЫБОР КОГДА");
	Шаблоны.Вставить("THEN", "ТОГДА");
	Шаблоны.Вставить("ELSE", "ИНАЧЕ");
	Шаблоны.Вставить("NOT ", "НЕ ");
	Шаблоны.Вставить("UNION ALL", "ОБЪЕДИНИТЬ ВСЕ");
	Шаблоны.Вставить("UNION", "ОБЪЕДИНИТЬ");	
	Шаблоны.Вставить("MAX", "МАКСИМУМ");
	Шаблоны.Вставить("MIN", "МИНИМУМ");
	Шаблоны.Вставить("CAST", "ВЫРАЗИТЬ");	
	Шаблоны.Вставить("AS ", "КАК ");
	Шаблоны.Вставить("NUMERIC", "ЧИСЛО");
	Шаблоны.Вставить("SET ", "УСТАНОВИТЬ ");
	Шаблоны.Вставить("TOP ", "ПЕРВЫЕ ");
	Шаблоны.Вставить("ALLOWED ", "РАЗРЕШЕННЫЕ ");
	Шаблоны.Вставить("END ", "КОНЕЦ ");
	Шаблоны.Вставить("SUM ", "СУММА ");
	Шаблоны.Вставить("HAVING ", "ИМЕЮЩИЕСЯ ");
	Шаблоны.Вставить("SUBSTR ", "ПОДСТРОКА ");
	Шаблоны.Вставить(" ALL ", " ВСЕ ");
	
	//Доп поля с которыми проблема
	Шаблоны.Вставить("._Period", ".Период");

	Возврат Шаблоны;
КонецФункции
	
#КонецОбласти
